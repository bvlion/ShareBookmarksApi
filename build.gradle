buildscript {
    repositories {
        gradlePluginPortal()
        google()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.20"
        classpath "com.google.cloud.tools:appengine-gradle-plugin:2.4.4"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}
apply from: 'ext.gradle'

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.google.cloud.tools.appengine'

group 'net.ambitious.sharebookmarks'
version '1.0.0'
mainClassName = "net.ambitious.sharebookmarks.ApplicationKt"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    gradlePluginPortal()
    google()
}

appengine {
    stage {
        setAppEngineDirectory(".")
    }
    deploy {
        def globalConfiguration = rootProject.extensions.getByName("ext")

        projectId = globalConfiguration["projectId"]
        version = globalConfiguration["versionCode"]
    }
}

static String captureVersion(Dependency dependency) {
    return dependency.version
}

dependencies {
    def ktor_version = captureVersion(implementation("io.ktor:ktor-server-netty:1.6.7"))
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.4.5"
    implementation "org.koin:koin-ktor:2.2.2"
    implementation "org.jetbrains.exposed:exposed:0.17.14"
    implementation "mysql:mysql-connector-java:8.0.30"
    implementation "org.jsoup:jsoup:1.15.2"
    implementation "com.google.cloud.sql:mysql-socket-factory:1.6.3"
    implementation "com.zaxxer:HikariCP:5.0.1"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.mockk:mockk:1.12.5"
    testImplementation "org.json:json:20231013"

}

kotlin.experimental.coroutines = 'enable'

task yamlCopy(type: Copy) {
    from 'secret.yaml'
    into 'build/staged-app'
}

shadowJar {
    classifier = null
}