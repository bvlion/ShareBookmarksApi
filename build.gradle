buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.cloud.tools:appengine-gradle-plugin:$appengine_plugin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}
apply from: 'ext.gradle'

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.google.cloud.tools.appengine'

group 'net.ambitious.sharebookmarks'
version '1.0.0'
mainClassName = "net.ambitious.sharebookmarks.ApplicationKt"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    gradlePluginPortal()
    google()
}

appengine {
    stage {
        setAppEngineDirectory(".")
    }
    deploy {
        def globalConfiguration = rootProject.extensions.getByName("ext")

        projectId = globalConfiguration["projectId"]
        version = globalConfiguration["versionCode"]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "org.koin:koin-ktor:$koin_version"
    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "mysql:mysql-connector-java:$mysql_connector_version"
    implementation "org.jsoup:jsoup:1.13.1"
    implementation "com.google.cloud.sql:mysql-socket-factory:1.2.0"
    implementation "com.zaxxer:HikariCP:4.0.3"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.mockk:mockk:1.10.5"
    testImplementation "org.json:json:20210307"

}

kotlin.experimental.coroutines = 'enable'

task yamlCopy(type: Copy) {
    from 'secret.yaml'
    into 'build/staged-app'
}

task stage {
    dependsOn assemble
}

appengineDeploy.dependsOn yamlCopy
